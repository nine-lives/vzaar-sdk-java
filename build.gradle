apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply from: "$rootDir/coverage.gradle"

Properties versionProperties = new Properties();
versionProperties.load(new File(rootDir, "src/main/resources/version.properties").newInputStream())
version = versionProperties.getProperty("version")
group = 'com.vzaar'
archivesBaseName = "vzaar-java-sdk"

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.6'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.apache.httpcomponents:httpmime:4.5.2'

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'

    checkstyle('com.puppycrawl.tools:checkstyle:7.4')
}

checkstyle {
    configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
    showViolations = false
}

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    systemProperties = [
            vzaarClientId: System.getProperty('vzaarClientId'),
            vzaarAuthToken: System.getProperty('vzaarAuthToken'),
    ]

    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '', password: project.hasProperty('ossrhPassword')? ossrhPassword : '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: (project.hasProperty('ossrhUsername') ? ossrhUsername : ''), password: (project.hasProperty('ossrhPassword')? ossrhPassword : ''))
            }

            pom.project {
               name 'vzaar-sdk-java'
               packaging 'jar'
               description 'Vzaar Client Java SDK'
               url 'https://github.com/nine-lives/vzaar-sdk-java'

               scm {
                   url 'scm:git@github.com:nine-lives/vzaar-sdk-java.git'
                   connection 'scm:git@github.com:nine-lives/vzaar-sdk-java.git'
                   developerConnection 'scm:git@github.com:nine-lives/vzaar-sdk-java.git'
               }

               licenses {
                   license {
                       name 'The MIT License (MIT)'
                       url 'https://raw.githubusercontent.com/nine-lives/vzaar-sdk-java/master/LICENSE'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'mgsmith57'
                       name 'Marc G. Smith'
                       email 'marc@vzaar.com'
                   }
               }
           }
        }
    }
}
